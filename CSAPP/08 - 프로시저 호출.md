# 프로시저 호출

프로시저 호출은 소프트웨어에서의 주요 추상화로 지정된 인자들과 리턴 값으로 특정 기능을 구현하는 코드를 감싸주는 방법을 제공한다. 프로시저는 프로그래밍 언어에서 여러 가지 다른 모습(함수, 메서드, 서브루틴, 핸들러 등)으로 사용된다. 그러나 모두 일반적인 특징들을 공유한다.

프로시저에 대한 기계어 수준 지원을 제공할 때 처리되어야 하는 여러 가지 많은 특성들이 존재한다. 논의를 풀어나가기 위해 프로시저 P가 프로시저 Q를 호출하고, Q가 실행한 후에 다시 P로 리턴한다고 가장하자. 이러한 동작들은 다음과 같은 하나 이상의 메커니즘이 연관된다:

- **제어 전달**: 프로그램 카운터는 진입할 때 Q에 대한 코드의 시작주소로 설정되고, 리턴할 때는 P에서 Q를 호출하는 인스트럭션 다음의 인스트럭션으로 설정되어야 한다. 
- **데이터 전달**: P는 하나 이상의 매개변수를 Q에 제공할 수 있어야 하며, Q는 다시 P로 하나의 값으로 리턴할 수 있어야 한다.
- **메모리 할당과 반납**: Q는 시작할 때 지역변수들을 위한 공간을 할당할 수도 있고, 리턴할 때 이 저장소를 반납할 수 있다.

---

## 1. 런타임 스택

대부분 언어에서 프로시저 호출 동작 방식의 주요 특징은 후입선출(LIFO) 메모리 관리 방식을 활용할 수 있다는 점이다. 프로시저 P가 프로시저 Q를 호출할 때, Q가 실행되고 있는 동안 P까지의 연속된 호출 중의 프로시저 P는 일시적으로 중지된다. Q가 실행되는 동안에는 자신의 지역변수를 위한 새로운 저장공간을 할당할 수 있는 능력이나 다른 프로시저로의 호출을 설정하는 능력만을 필요로 한다. 반대로 Q가 리턴할 때는 자신이 할당받은 로컬 저장장소는 반납할 수 있다. 따라서 프로그램은 스택을 사용해서 프로시저들이 요구하는 저장장소를 관리할 수 있으며, 여기서 스택과 프로그램 레지스터들은 제어와 데이터를 전송하기 위해, 그리고 메모리를 할당하는 데 필요한 정보를 저장한다.

--정보 접근과 연산—에서 설명한 것처럼 x86-64의 스택은 작은 주소 방향으로 성장하며, 스택 포인터 `%rsp`는 스택의 최상위 원소를 가리킨다. 데이터는 `pushq`와 `popq` 인스터럭션을 이용해서 스택에 저장하고 읽어올 수 있다.

— 사진 --

x86-64 프로시저가 레지스터들에 저장할 수 있는 개수 이상의 저장공간을 필요로 할 때는 공간을 스택에 할당한다. 이 저장공간을 프로시저의 **스택 프레임**이라고 부른다. 위 그림은 런타임 스택의 전체적인 구조를 스택 프레임들로 나눈 걸 보여준다.

프로시저 P가 Q를 호출할 때 **리턴 주소**(return address)를 스택에 푸시해서 Q가 리턴할 때 P에서 프로그램이 실행을 재시작해야 하는 위치를 가리킨다. 리턴 주소가 P에 관계된 상태들을 저장하기 때문에 리턴 주소는 P의 스택 프레임에 속하는 것으로 간주한다. Q에 대한 코드는 현재 스택 경계를 확장해서 자신의 스택 프레임을 위한 공간을 할당한다. 이 공간 내에서 레지스터 값들을 저장하고, 지역변수들을 위한 공간을 할당하며, 자신이 호출하는 프로시저들을 위한 인자들을 설정할 수 있다.

대부분의 스택 프레임들은 프로시저가 시작될 때 할당되는 고정 크기를 가진다. 프로시저 P는 최대 여섯 개의 정수값들을 레지스터로 전송할 수 있지만, 만일 Q가 더 많은 인자를 요구한다면 호출하기 전에 자신의 스택 프레임 내에 P에 의해 저장될 수 있다.

시간과 공간 효율성을 위해 x86-64 프로시저는 요청받은 스택 프레임의 일부분만 할당한다. 예를 들어, 여섯 개 이하 인자를 가진 프로시저의 매개변수들은 레지스터로 전달될 수 있으므로 생략될 수 있다. 심지어 모든 지역변수들을 레지스터에 보관할 수 있고 이 함수가 다른 함수를 하나도 호출하지 않으면 스택 프레임을 요청하지도 않는다.

---

## 2. 제어의 이동

제어를 프로시저 P에서 프로시저 Q로 전달하는 것은 단순히 PC(프로그램 카운터)를 Q의 시작 주소로 설정하는 것이다. 하지만 나중에 Q가 리턴해야 할 때 프로세서는 P의 실행을 다시 시작하기 위해서 코드 위치의 일부 기록을 가지고 있어야 한다. 이 정보는 x86-64 머신에서는 인스트럭션 `call Q`로 프로시저 Q를 호출해서 기록된다.  이 인스터력션은 주소 A를 스택에 푸시하고 PC를 Q의 시작으로 설정한다. 푸시된 주소 *A*는 **리턴 주소**라고 불리며, `call` 인스트럭션 바로 다음 인스트럭션의 주소로 계산된다. 이에 대응하는 인스트럭션 `ret`은 주소 *A*를 스택에서 팝해오고 PC를 *A*로 설정한다.

| Instruction   | Description      |
| ------------- | ---------------- |
| call Label    | Procedure call   |
| call *Operand | Procedure call   |
| ret           | Return from call |

`call` 인스트럭션은 호출된 프로시저가 시작하는 인스트럭션의 주소를 목적지로 갖는다. `jmp`와 유사하게 직접 또는 간접형태를 갖는다. 

```cpp
void multstore(long, long, long*)

int main(O)
{
	long d;
    multstore(2, 3 &d);
    
    return 0;
}
```

```
// Beginning of function multstore
0000000000400540 <multstore>
  400540: 53              // push %rbx
  400541: 48 89 df        // mov  %rdx, $rbx
  ...
// Return from function multstore
  40054d: c3              // retq
  ...
// Call to multstore from main
  400563: e8 d8 ff ff ff  // callq 400540 <multstore>
  400568: 48 8b 54 24 08  // mov  0x8(%rsp), %rdx
```

위 코드에서 *main*의 주소 0x400563 에서  `call` 인스트럭션은 프로시저 *multstore*를 호출한다. `call`의 효과는 리턴 주소 0x400568을 스택에 저장하고 인자인 *mulststore* 프로시저의 첫 번째 인스트럭션 주소인 0x400540으로 점프한다. 프로시저  *multstore*는 주소 0x40054d의 `ret` 인스트럭션을 만날 떄가지 실행된다. 이 `ret` 인스트럭션은 스택에서 0x400568을 pop해서 이 주소로 점프한 후에 *main*의 실행을 재개한다.

|                | %rip (PC) | %rsp (stack pointer) | Stack    |
| -------------- | --------- | -------------------- | -------- |
| Executing call | 0x400563  | 0x7fffffffe840       |          |
| After call     | 0x400540  | 0x7fffffffe838       | 0x400568 |
| After ret      | 0x400568  | 0x7fffffffe840       |          |

이렇게 리턴 주소를 스택에 푸시하는 간단한 방법을 사용해서 프로시저가 나중에 프로그램의 적절한 위치로 리턴이 가능하게 된다는 것을 알 수 있다. C 언어의 표준 콜/리턴 동작 방식은 위 방식과 같다.

---

## 3. 데이터 전송

프로시저 호출과 리턴은 제어 전달뿐만 아니라  데이터 전달과도 관련되어 있다. x86-64에서 프로시저로, 프로시저부터의 데이터 전달은 레지스터를 통해서 일어난다. 프로시저 P가 프로시저 Q를 호출할 때, P에 대한 코드는 먼저 인자들을 적절한 레지스터들에 복사해야 한다. 유사하게 Q가 P로 다시 리턴할 때, P에 대한 코드는 리탄 값을 레지스터 `%rax`에서 접근할 수 있다.

x86-64에서는 최대 여섯 개의 정수형 인자가 레지스터로 전달될 수 있다. 이 레지스터들은 전달되는 데이터형의 길이에 따라 레지스터 이름을 이용해서 정해진 순서로 이용된다.

|      | 1    | 2    | 3    | 4    | 5    | 6    |
| ---- | ---- | ---- | ---- | ---- | ---- | ---- |
| 64   | %rdi | %rsi | %rdx | %rcx | %r8  | %r9  |
| 32   | %edi | %esi | %edx | %ecx | %r8d | %r9  |
| 16   | %di  | %si  | %dx  | %cx  | %r8w | %r9w |
| 8    | %dil | %sil | %dl  | %cl  | %r8b | %r9b |

함수가 여섯 개 이상의 정수형 인자를 가질 때, 다른 인자들은 스택으로 전달된다. n > 6인 n개의 정수형 인자를 가지면서 호출하면, 7에서 n까지를 위한 충분한 크기의 저장공간을 스택 프레임에 할당해야 한다. 인자 1~6은 적절한 레지스터들에 복사하고, 인자 7에서 n까지는 스택 탑에 넣는 방법으로 저장한다. 매개변수들을 스택으로 전달할 때, 모든 데이터 길이는 8의 배수로 반올림된다. 스택 프레임에서 "Argument build area" 영역으로 이 공간이 할당된다.

```cpp
// C code
void porc(long a1, long* a1p,
         int a2, int* a2p,
         short a3, short* a3p,
         char a4, char* a4p)
{
    *a1p += a1;
    *a2p += a2;
    *a3p += a3;
    *a4p += a4;
}
```

```assembly
// Generated assembly code
// void proc(a1, a1p, a2, a2p, a3, a3p, a4, a4p)
// a1  in %rdi     (64 bits)
// a1p in %rsi     (64 bits)
// a2  in %edx     (32 bits)
// a2p in %rcx     (64 bits)
// a3  in %r8w     (16 bits)
// a3p in %r9      (64 bits)
// a4  at %rsp+8   ( 8 bits)
// a4p at %rsp+16  (64 bits)
proc:
	movq 16(%rsp), %rax   // Fetch a4p  (64 bits)
	addq %rdi, (%rsi)     // *a1p += a1 (64 bits)
	addl %edx, (%rcx)     // *a2p += a2 (32 bits)
	addw %r8w, (%r9)      // *a3p += a3 (16 bits)
	movl 8(%rsp), %edx    // Fetch a4   ( 8 bits)
	addb %dl, (%rax)      // *a4p += a4 ( 8 bits)
	ret
```

---

## 4. 스택에서의 지역 저장 공간

지역변수가 메모리에 저장되어야 하는 경우 다음의 공통적인 경우를 포함한다:

- 지역변수 모두를 저장하기에는 레지스터의 수가 부족하다.
- 지역변수에 연산자 '&'가 사용되었으며, 이 변수의 주소를 생성할 수 있어야 한다.
- 일부 지역변수들이 배열 또는 구조체여서 이들이 배열이나 구조체 참조로 접근되어야 한다.

일반적으로 프로시저는 스택 포인터를 감소시켜서 스택 프레임에 공간을 할당한다. 이렇게 하면 "Local variables"로 명명된 스택 프레임의 일부분이 생겨난다.

```cpp
long swap_add(long* xp, long* yp)
{
    long x = *xp;
    long y = *yp;
    *xp = y;
    *yp = x;
    return x + y;
}

long caller()
{
    long arg1 = 534;
    long arg2 = 1057;
    long sum = swap_add(&arg1, &arg2);
    long diff = arg1 - arg2;
    return sum * diff;
}
```

```assembly
//long caller()
caller:
	subq  $16, %rsp              // Allocate 16 bytes for stack frame
	movq  $534, (%rsp)           // Store 534 in arg1
	movq  $1057, 8(%rsp)         // Store 1057 in arg2
	leaq  8(%rsp), %rsi          // Compute &arg2 as second argument
	moveq %rsp, %rdi             // Compute &arg1 as first argument
	call  swapp_add              // Call swap_add(&arg1, &arg2)
	moveq (%rsp), %rdx           // Get arg1
	subq  8(%rsp), %rdx          // Compute diff = arg1 - arg2
	imulq %rdx, %rax             // Compute sum *diff
	addq  $16, $rsp              // Deallocate stack frame
	ret                          // Return
```

위 코드는 caller(호출자)가 스택 프레임을 이용해서 어떻게 지역변수를 구현하는지를 보여준다. 코드는 스택 포인터를 16 감소시키는 것으로 시작한다. 이것은 결국 스택에 16바이트를 할당하는 것을 의미한다. *S*를 스택 포인터의 값이라고 하면, &arg2를 *S* 8, &arg1을 *S*로 계산한다는 것을 알 수 있다.

---

## 5. 레지스터를 이용하는 지역 저장소

관습적으로 `%rbx`, `%rbp`, `%r12` ~ `%r15`는 callee-saved 레지스터다. 프로시저 P가 프로시저 Q를 호출할 때, Q는 Q가 P로 리턴될 때 Q가 호출되었을 때의 값들과 같도록 보장할 수 있게 이 레지스터들의 값을 **보존해야** 한다. 프로시저 Q는 이 값을 전혀 변경하지 않거나 원래의 값을 스택에 푸시해두고 이 값을 변경하며, 리턴하기 전에 스택에서 이전 값을 팝해오는 방식으로 레지스터를 보존한다. 레지스터를 값들을 푸시하는 것은 "Saved registers"로 이름 붙인 스택 프레임의 일부분을 생성하는 효과를 갖는다.

```cpp
// Calling function
long P(long x, long y)
{
    long u = Q(y);
    long v = Q(x);
    return u + v;
}
```

```assembly
// Generated assembly code for calling function
// long P(long x, long y)
// x in %rdi, y in %rsi
P:
	pushq   %rbp		// Save %rbp
	pushq   %rbx        // Save %rbx
	subq	$8, %rsp	// Align stack frame
	movq	%rdi, %rbp  // Save x
	movq	%rsi, %rdi  // Move y to first argument
	call	Q           // Call Q(y)
	movq 	%rax, %rbx  // Save result
	movq 	%rbp, %rdi	// Move x to first argument
	call	Q			// Call Q(x)
	addq	%rbx, %rax	// Add saved Q(y) to Q(x)
	addq	$8, %rsp	// Dealloate last part of stack
	popq	%rbx		// Restore %rbx
	popq	%rbp		// Restore %rbp
	ret
```

위 예제는 Q 프로시저를 두 번 호출한다. 첫 번째 호출 동안에 *x* 값을 다음에 사용하기 위해 유지해야 하고, 두 번째 호출 동안 Q(*y*)를 위해 계산된 값을 유지해야 한다. 위 생성된 어셈블리 코드를 보면 두 개의 callee-saved 레지스터들을 사용한다는 거를 알 수 있다. `%rbp`는 *x*를 보관하고, `%rbx`는 Q(*y*)를 계산한 값을 저장한다.

---

## 6. 재귀 프로시저

레지스터와 스택을 사용하는 것에 대한 설명으로 x86-64 프로시저들이 재귀적으로 호출하는 것을 설명할 수 있다. 각 프로시저 콜은 스택 스택상에 자신만의 사적인 공간을 가지며, 따라서 다수의 별도 호출들의 지역변수들은 서로 간섭하지 않는다. 그뿐만 아니라, 스택 운영방식은 프로시저가 호출될 때 지역 저장소를 할당하고, 리턴하기 전에 이것을 반환하는 적절할 정책을 제공한다.

재귀적인 팩포리털 프로그램 코드:

```cpp
// C code
long rfact(long n)
{
    long result;
    if (n = 1)
        result = 1;
    else
        result = n * rfact(n-1);
    
    return result;
}
```

```assembly
// Generated assembly code
// long rfact(long n)
// n in %rdi
rfact:
	pushq   %rbx			// Save %rbx
	movq	%rdi, %rbx		// Store n in callee-saved register
	movl	$1, $eax		// Set return value = 1
	cmpq	$1, $rdi		// Compare n:1
	jle		.L35			// If <=, goto done
	leaq	-1(%rdi), %rdi  // Compute n-1
	call 	rfact			// Call rfact(n-1)
	imulq	%rbx, %rax		// Multiply result by n
  .L35:				// done:
  	popq 	%rbx			// Restore %rbx
  	ret						// Return
```

위 예제를 통해 어셈블리 코드가 먼저 스택에 기존 값을 보관한 후에 레지스터 `%rbx`를 매개변수 *n*을 저장하기 위해 사용하며, 나중에 리턴하기 전에 그 값을 복원하는 것을 알 수 있다.

스택 기법을 사용해서 함수의 각 호출 시에 상태정보(리턴 위치의 저장된 값, callee-saved 레지스터)를 위한 자신만의 저장공간을 제공한다. 필요한 경우에는 지역변수를 위한 저장공간도 제공한다.

---